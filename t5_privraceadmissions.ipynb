{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Table 5: Non-Latinx First-Year Enrollments at Private Institutions with Admissions Control\n",
    "\n",
    "from White Flight in Higher Education? The Case of Hispanic-Serving Institutions\n",
    "\n",
    "by Laura Hamilton, Charlie Eaton, and Simon Cheng"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      ". sum beforeyrs afteryrs beforeafteryrs\n",
      "\n",
      "    Variable |        Obs        Mean    Std. Dev.       Min        Max\n",
      "-------------+---------------------------------------------------------\n",
      "   beforeyrs |     16,054   -.3405382    1.848044        -18          0\n",
      "    afteryrs |     16,054    .0759935    .7226518          0         17\n",
      "beforeafte~s |     16,054   -.2645447    1.997311        -18         17\n"
     ]
    }
   ],
   "source": [
    "quietly {\n",
    "\n",
    "use data/hdef19902019, clear\n",
    "    \n",
    "**Create separate unitid for Benjamin Franklin Institute for when it goes from Public to Private\n",
    "    **Correct IPEDS reporting error to note it is private in 2016 & 2017\n",
    "replace unitid=unitid+1000000 if unitid==165884 & year>2007\n",
    "    replace control=2 if unitid==1165884\n",
    "    \n",
    "*replace Boricuo college Latinx enrollment as missing for years 1993 & 1994\n",
    "    * when reporting error reduced % Latinx from 95% to 0%\n",
    "replace tefhispt=. if unitid==189413 & year<1995 & year>1992\n",
    "    \n",
    "keep if ftfeftotlt>100 & ftfeftotlt<. & control==2\n",
    "    \n",
    "keep if admitpct!=.\n",
    "    \n",
    "foreach var in efbkaat efhispt efwhitt efaiant efasiat {\n",
    "quietly    gen pctt`var'= t`var' / teftotlt * 100\n",
    "quietly gen lnftf`var'=log(ftf`var')\n",
    "    quietly gen lnt`var'=log(t`var')\n",
    "}\n",
    "foreach var in cpopam cpopas cpopbl cpopwh cpophi populationam populationas populationbl populationhi populationwh {\n",
    "        gen ln`var'=log(`var')\n",
    "    }\n",
    "\n",
    "quietly gen lnftfeftotlt=log(ftfeftotlt)\n",
    "\n",
    "quietly gen hispserve=0 if pcttefhispt!=.\n",
    "quietly replace hispserve=1 if pcttefhispt>=25 & pcttefhispt<.\n",
    "\n",
    "xtset unitid year\n",
    "\n",
    "**create var for year in which event occurs observation year of event for subject**\n",
    "by unitid: gen transition=year if hispserv==1 & l.hispserv==0\n",
    "\n",
    "**create var for year in which event occurs in all observations for all years for subject**\n",
    "by unitid: egen hispserveyear = min(transition)\n",
    "\n",
    "**generate a var for the number of years relative to the year of the event (e.g. + or - 2 years)\n",
    "\n",
    "gen hispserveyeardiff= year-hispserveyear\n",
    "    \n",
    "**bin up year differences larger than absolute value of 10**\n",
    "*replace hispserveyeardiff =10 if hispserveyeardiff <9999 & hispserveyeardiff >10\n",
    "*replace hispserveyeardiff =-10 if hispserveyeardiff <-10\n",
    "    \n",
    "    gen afteryrs=0\n",
    "replace afteryrs=hispserveyeardiff if hispserveyeardiff>0 & hispserveyeardiff<.\n",
    "gen beforeyrs=0\n",
    "replace beforeyrs=hispserveyeardiff if hispserveyeardiff<0\n",
    "gen beforeafteryrs=0\n",
    "replace beforeafteryrs=hispserveyeardiff if hispserveyeardiff!=.\n",
    "    \n",
    "gen switchers = (hispserveyeardiff<.)\n",
    "    label var beforeyrs \"before years\"\n",
    "    label var afteryrs \"after years\"\n",
    "    label var beforeafteryrs \"before and after years\"\n",
    "    label var lnftfefwhitt \"white\"\n",
    "    label var lnftfefasiat \"Asian\"\n",
    "    label var lnftfefbkaat \"Black\"\n",
    "    label var admitpct \"% admitted\"\n",
    "}\n",
    "sum beforeyrs afteryrs beforeafteryrs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      "\n",
      "\n",
      "\n",
      "white enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years             -0.032*       -0.004        -0.013**                                           \n",
      "                        (0.013)       (0.007)       (0.005)                                             \n",
      "\n",
      "after years              -0.063***     -0.026***     -0.042***     -0.031***     -0.022*       -0.029***\n",
      "                        (0.011)       (0.006)       (0.004)       (0.009)       (0.010)       (0.008)   \n",
      "\n",
      "before and after y~s                                               -0.032*       -0.004        -0.013** \n",
      "                                                                  (0.013)       (0.007)       (0.005)   \n",
      "\n",
      "% admitted               -0.001        -0.001         0.000        -0.001        -0.001         0.000   \n",
      "                        (0.001)       (0.001)       (0.000)       (0.001)       (0.001)       (0.000)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.54          0.53          0.44          0.54          0.53          0.44   \n",
      "schools                      58           190           963            58           190           963   \n",
      "N                         1,037         1,648        15,789         1,037         1,646        15,789   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "\n",
      "Asian enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years              0.020        -0.003        -0.004                                             \n",
      "                        (0.020)       (0.011)       (0.007)                                             \n",
      "\n",
      "after years              -0.020        -0.020        -0.037**      -0.040**      -0.017        -0.034*  \n",
      "                        (0.020)       (0.013)       (0.011)       (0.015)       (0.017)       (0.015)   \n",
      "\n",
      "before and after y~s                                                0.020        -0.003        -0.004   \n",
      "                                                                  (0.020)       (0.011)       (0.007)   \n",
      "\n",
      "% admitted                0.001         0.001         0.000         0.001         0.001         0.000   \n",
      "                        (0.002)       (0.001)       (0.000)       (0.002)       (0.001)       (0.000)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.29          0.20          0.14          0.29          0.20          0.14   \n",
      "schools                      58           186           943            58           186           943   \n",
      "N                           995         1,573        14,553           995         1,571        14,553   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "\n",
      "Black enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years             -0.000        -0.000        -0.012                                             \n",
      "                        (0.017)       (0.009)       (0.006)                                             \n",
      "\n",
      "after years              -0.004        -0.010        -0.020        -0.004        -0.009        -0.008   \n",
      "                        (0.022)       (0.011)       (0.013)       (0.015)       (0.012)       (0.014)   \n",
      "\n",
      "before and after y~s                                               -0.000        -0.002        -0.012   \n",
      "                                                                  (0.017)       (0.009)       (0.006)   \n",
      "\n",
      "% admitted                0.001        -0.000        -0.000         0.001        -0.000        -0.000   \n",
      "                        (0.001)       (0.001)       (0.000)       (0.001)       (0.001)       (0.000)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.34          0.38          0.31          0.34          0.38          0.31   \n",
      "schools                      58           189           955            58           189           955   \n",
      "N                         1,039         1,647        15,825         1,039         1,645        15,825   \n",
      "--------------------------------------------------------------------------------------------------------\n"
     ]
    }
   ],
   "source": [
    "*** Check and clear xt\n",
    "\n",
    "    qui xtset\n",
    "    qui set line 105\n",
    "    qui set more off\n",
    "\n",
    " ** Private schools, Y is logged\n",
    "    foreach nam in ftfefwhitt ftfefasiat ftfefbkaat {\n",
    "        \n",
    "est clear\n",
    "        qui eststo SwPost: reghdfe ln`nam' beforeyrs afteryrs lnftfeftotlt admitpct ///\n",
    "        lnpop* lncpop* i.year if switchers==1, absorb(unitid) cluster(unitid)\n",
    "\n",
    "        qui eststo counterPost: reghdfe ln`nam' beforeyrs afteryrs lnftfeftotlt admitpct ///\n",
    "        lnpop* lncpop* i.year if pcttefhispt>=15, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo NSwPost: reghdfe ln`nam' beforeyrs afteryrs lnftfeftotlt admitpct ///\n",
    "        lnpop* lncpop* i.year, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo Swdiff: reghdfe ln`nam' afteryrs beforeafteryrs lnftfeftotlt admitpct ///\n",
    "        lnpop* lncpop* i.year if switchers==1, absorb(unitid) cluster(unitid)\n",
    "\n",
    "        qui eststo counterdiff: reghdfe ln`nam' afteryrs beforeafteryrs lnftfeftotlt admitpct ///\n",
    "        lnpop* lncpop* i.year if pcttefhispt>15, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo NSwdiff: reghdfe ln`nam' afteryrs beforeafteryrs lnftfeftotlt admitpct ///\n",
    "        lnpop* lncpop* i.year, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        esttab SwPost counterPost NSwPost Swdiff counterdiff NSwdiff, star(* .05 ** .01 *** .001) ///\n",
    "        stats(r2_a_within N_clust N, fmt(2 %9.0fc %9.0fc) label(\"r2\" \"schools\" \"N\")) ///\n",
    "        keep(beforeyrs afteryrs beforeafteryrs admitpct) se(%9.3fc) b(%9.3fc) ///        \n",
    "        order(beforeyrs afteryrs beforeafteryrs admitpct) ///\n",
    "        label mtitle(\"Switchers\" \"Counterfac\" \"All\" \"Switchers\" \"Counterfac \"\"All\") ///\n",
    "        prehead(\"`: var label ln`nam'' enrollment\") nonotes modelwidth(10)\n",
    "            \n",
    "   qui esttab SwPost counterPost NSwPost Swdiff counterdiff NSwdiff using tables/t5_privadmit`nam'.rtf, star(* .05 ** .01 *** .001) ///\n",
    "        stats(r2_a_within N_clust N, fmt(2 %9.0fc %9.0fc) label(\"r2\" \"schools\" \"N\")) ///\n",
    "        keep(beforeyrs afteryrs beforeafteryrs admitpct) se(%9.3fc) b(%9.3fc) ///\n",
    "        order(beforeyrs afteryrs beforeafteryrs admitpct) ///\n",
    "        label mtitle(\"Switchers\" \"Counter- factual\" \"All\" \"Switchers\" \"Counter- factual \"\"All\") replace ///\n",
    "        title(\"\\b `: var label ln`nam'' enrollment\") nonotes onecell compress\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Stata",
   "language": "stata",
   "name": "stata"
  },
  "language_info": {
   "codemirror_mode": "stata",
   "file_extension": ".do",
   "mimetype": "text/x-stata",
   "name": "stata",
   "version": "15.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
