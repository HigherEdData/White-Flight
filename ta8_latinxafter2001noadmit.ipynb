{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Appendix Table A8: Latinx First-Year Enrollments Since 2001 Without Admissions Control\n",
    "\n",
    "from White Flight in Higher Education? The Case of Hispanic-Serving Institutions\n",
    "\n",
    "by Laura Hamilton, Charlie Eaton, and Simon Cheng"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      ". sum beforeyrs afteryrs beforeafteryrs\n",
      "\n",
      "    Variable |        Obs        Mean    Std. Dev.       Min        Max\n",
      "-------------+---------------------------------------------------------\n",
      "   beforeyrs |     29,233   -.3164916    1.563207        -10          0\n",
      "    afteryrs |     29,233    .1045736    .8157278          0         10\n",
      "beforeafte~s |     29,233    -.211918    1.781916        -10         10\n"
     ]
    }
   ],
   "source": [
    "quietly {\n",
    "\n",
    "use /Users/Charlie/Dropbox/Stata_Notebooks/racedebtstrat/hdef19802019.dta, clear\n",
    "keep if control<3 & iclevel==1 & year>1989 & ftfeftotlt>100 & ftfeftotlt<.\n",
    "    \n",
    "keep if year>2000.\n",
    "    gen pubpriv=\"public\"\n",
    "    replace pubpriv=\"private\" if control==2\n",
    "    \n",
    "foreach var in efbkaat efhispt efwhitt efaiant efasiat {\n",
    "quietly    gen pctt`var'= t`var' / teftotlt * 100\n",
    "quietly gen lnftf`var'=log(ftf`var')\n",
    "    quietly gen lnt`var'=log(t`var')\n",
    "}\n",
    "foreach var in cpopam cpopas cpopbl cpopwh cpophi populationam populationas populationbl populationhi populationwh {\n",
    "        gen ln`var'=log(`var')\n",
    "    }\n",
    "\n",
    "quietly gen lnftfeftotlt=log(ftfeftotlt)\n",
    "\n",
    "**More robust would be discontinuity\n",
    "quietly gen hispserve=0 if pcttefhispt!=.\n",
    "quietly replace hispserve=1 if pcttefhispt>=25 & pcttefhispt<.\n",
    "\n",
    "xtset unitid year\n",
    "\n",
    "**create var for year in which event occurs observation year of event for subject**\n",
    "by unitid: gen transition=year if hispserv==1 & l.hispserv==0\n",
    "\n",
    "**create var for year in which event occurs in all observations for all years for subject**\n",
    "by unitid: egen hispserveyear = min(transition)\n",
    "\n",
    "**generate a var for the number of years relative to the year of the event (e.g. + or - 2 years)\n",
    "\n",
    "gen hispserveyeardiff= year-hispserveyear\n",
    "    \n",
    "**bin up year differences larger than absolute value of 10**\n",
    "replace hispserveyeardiff =10 if hispserveyeardiff <9999 & hispserveyeardiff >10\n",
    "replace hispserveyeardiff =-10 if hispserveyeardiff <-10\n",
    "    \n",
    "    gen afteryrs=0\n",
    "replace afteryrs=hispserveyeardiff if hispserveyeardiff>0 & hispserveyeardiff<.\n",
    "gen beforeyrs=0\n",
    "replace beforeyrs=hispserveyeardiff if hispserveyeardiff<0\n",
    "gen beforeafteryrs=0\n",
    "replace beforeafteryrs=hispserveyeardiff if hispserveyeardiff!=.\n",
    "    \n",
    "    replace hispserveyeardiff=hispserveyeardiff+10\n",
    "    \n",
    "gen switchers = (hispserveyeardiff<.)\n",
    "    label var beforeyrs \"before years\"\n",
    "    label var afteryrs \"after years\"\n",
    "    label var beforeafteryrs \"before and after years\"\n",
    "    label var lnftfefwhitt \"white\"\n",
    "    label var lnftfefasiat \"Asian\"\n",
    "    label var lnftfefbkaat \"Black\"\n",
    "    label var admitpct \"% admitted\"\n",
    "}\n",
    "sum beforeyrs afteryrs beforeafteryrs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      "\n",
      "\n",
      "\n",
      "public Latinx enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years             -0.000         0.022***     -0.014**                                           \n",
      "                        (0.009)       (0.004)       (0.005)                                             \n",
      "\n",
      "after years              -0.027*        0.016***     -0.026***     -0.026***     -0.006        -0.012   \n",
      "                        (0.010)       (0.004)       (0.006)       (0.006)       (0.006)       (0.008)   \n",
      "\n",
      "before and after y~s                                               -0.000         0.022***     -0.014** \n",
      "                                                                  (0.009)       (0.004)       (0.005)   \n",
      "\n",
      "% admitted                                                                                              \n",
      "                                                                                                        \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.92          0.88          0.68          0.92          0.88          0.68   \n",
      "schools                      68           213           720            68           213           720   \n",
      "N                         1,138         2,136        11,614         1,138         2,136        11,614   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "\n",
      "private Latinx enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years              0.011         0.027***      0.003                                             \n",
      "                        (0.014)       (0.006)       (0.007)                                             \n",
      "\n",
      "after years              -0.010         0.036***     -0.008        -0.021*        0.009        -0.011   \n",
      "                        (0.017)       (0.006)       (0.007)       (0.009)       (0.008)       (0.011)   \n",
      "\n",
      "before and after y~s                                                0.011         0.027***      0.003   \n",
      "                                                                  (0.014)       (0.006)       (0.007)   \n",
      "\n",
      "% admitted                                                                                              \n",
      "                                                                                                        \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.76          0.75          0.49          0.76          0.75          0.49   \n",
      "schools                      64           215         1,039            64           215         1,039   \n",
      "N                         1,110         1,840        16,791         1,110         1,838        16,791   \n",
      "--------------------------------------------------------------------------------------------------------\n"
     ]
    }
   ],
   "source": [
    "*** Check and clear xt\n",
    "\n",
    "    qui xtset\n",
    "    qui set line 105\n",
    "    qui set more off\n",
    "\n",
    "foreach pubpriv in public private {\n",
    "\n",
    "est clear\n",
    "        qui eststo SwPost: reghdfe lnftfefhispt beforeyrs afteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if switchers==1 & pubpriv==\"`pubpriv'\", absorb(unitid) cluster(unitid)\n",
    "\n",
    "        qui eststo counterPost: reghdfe lnftfefhispt beforeyrs afteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if pcttefhispt>=15 & pubpriv==\"`pubpriv'\", absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo NSwPost: reghdfe lnftfefhispt beforeyrs afteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if pubpriv==\"`pubpriv'\", absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo Swdiff: reghdfe lnftfefhispt afteryrs beforeafteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if switchers==1 & pubpriv==\"`pubpriv'\", absorb(unitid) cluster(unitid)\n",
    "\n",
    "        qui eststo counterdiff: reghdfe lnftfefhispt afteryrs beforeafteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if pcttefhispt>15 & pubpriv==\"`pubpriv'\", absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo NSwdiff: reghdfe lnftfefhispt afteryrs beforeafteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if pubpriv==\"`pubpriv'\", absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        esttab SwPost counterPost NSwPost Swdiff counterdiff NSwdiff, star(* .05 ** .01 *** .001) ///\n",
    "        stats(r2_a_within N_clust N, fmt(2 %9.0fc %9.0fc) label(\"r2\" \"schools\" \"N\")) ///\n",
    "        keep(beforeyrs afteryrs beforeafteryrs admitpct) se(%9.3fc) b(%9.3fc) ///        \n",
    "        order(beforeyrs afteryrs beforeafteryrs admitpct) ///\n",
    "        label mtitle(\"Switchers\" \"Counterfac\" \"All\" \"Switchers\" \"Counterfac \"\"All\") ///\n",
    "        prehead(\"`pubpriv' Latinx enrollment\") nonotes modelwidth(10)\n",
    "            \n",
    "   qui esttab SwPost counterPost NSwPost Swdiff counterdiff NSwdiff using tables/ta8_latinx`pubpriv'noadmit.rtf, star(* .05 ** .01 *** .001) ///\n",
    "        stats(r2_a_within N_clust N, fmt(2 %9.0fc %9.0fc) label(\"r2\" \"schools\" \"N\")) ///\n",
    "        keep(beforeyrs afteryrs beforeafteryrs admitpct) se(%9.3fc) b(%9.3fc) ///\n",
    "        order(beforeyrs afteryrs beforeafteryrs admitpct) ///\n",
    "        label mtitle(\"Switchers\" \"Counter- factual\" \"All\" \"Switchers\" \"Counter- factual \"\"All\") replace ///\n",
    "        title(\"\\b `pubpriv' Latinx enrollment\") nonotes onecell compress\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Stata",
   "language": "stata",
   "name": "stata"
  },
  "language_info": {
   "codemirror_mode": "stata",
   "file_extension": ".do",
   "mimetype": "text/x-stata",
   "name": "stata",
   "version": "15.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
