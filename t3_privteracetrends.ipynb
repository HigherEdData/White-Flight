{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Table 3: Non-Latinx First-Year Enrollments and Years Before and After HSI Status at Private Institutions\n",
    "\n",
    "from White Flight in Higher Education? The Case of Hispanic-Serving Institutions\n",
    "\n",
    "by Laura Hamilton, Charlie Eaton, and Simon Cheng"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      ". sum beforeyrs afteryrs beforeafteryrs\n",
      "\n",
      "    Variable |        Obs        Mean    Std. Dev.       Min        Max\n",
      "-------------+---------------------------------------------------------\n",
      "   beforeyrs |     26,996   -.5493777     2.94308        -29          0\n",
      "    afteryrs |     26,996     .155949    1.392668          0         27\n",
      "beforeafte~s |     26,996   -.3934287    3.282164        -29         27\n"
     ]
    }
   ],
   "source": [
    "quietly {\n",
    "\n",
    "use data/hdef19902019, clear\n",
    "\n",
    "**Create separate unitid for Benjamin Franklin Institute for when it goes from Public to Private\n",
    "    **Correct IPEDS reporting error to note it is private in 2016 & 2017\n",
    "replace unitid=unitid+1000000 if unitid==165884 & year>2007\n",
    "    replace control=2 if unitid==1165884\n",
    "    \n",
    "*replace Boricuo college Latinx enrollment as missing for years 1993 & 1994\n",
    "    * when reporting error reduced % Latinx from 95% to 0%\n",
    "replace tefhispt=. if unitid==189413 & year<1995 & year>1992\n",
    "    \n",
    "keep if ftfeftotlt>100 & ftfeftotlt<. & control==2\n",
    "    \n",
    "foreach var in efbkaat efhispt efwhitt efaiant efasiat {\n",
    "quietly    gen pctt`var'= t`var' / teftotlt * 100\n",
    "quietly gen lnftf`var'=log(ftf`var')\n",
    "    quietly gen lnt`var'=log(t`var')\n",
    "}\n",
    "foreach var in cpopam cpopas cpopbl cpopwh cpophi populationam populationas populationbl populationhi populationwh {\n",
    "        gen ln`var'=log(`var')\n",
    "    }\n",
    "\n",
    "quietly gen lnftfeftotlt=log(ftfeftotlt)\n",
    "\n",
    "quietly gen hispserve=0 if pcttefhispt!=.\n",
    "quietly replace hispserve=1 if pcttefhispt>=25 & pcttefhispt<.\n",
    "\n",
    "xtset unitid year\n",
    "\n",
    "**create var for year in which event occurs observation year of event for subject**\n",
    "by unitid: gen transition=year if hispserv==1 & l.hispserv==0\n",
    "\n",
    "**create var for year in which event occurs in all observations for all years for subject**\n",
    "by unitid: egen hispserveyear = min(transition)\n",
    "\n",
    "**generate a var for the number of years relative to the year of the event (e.g. + or - 2 years)\n",
    "\n",
    "gen hispserveyeardiff= year-hispserveyear\n",
    "    \n",
    "*bin up year differences larger than absolute value of 10**\n",
    "*replace hispserveyeardiff =10 if hispserveyeardiff <9999 & hispserveyeardiff >10\n",
    "*replace hispserveyeardiff =-10 if hispserveyeardiff <-10\n",
    "    \n",
    "    gen afteryrs=0\n",
    "replace afteryrs=hispserveyeardiff if hispserveyeardiff>0 & hispserveyeardiff<.\n",
    "gen beforeyrs=0\n",
    "replace beforeyrs=hispserveyeardiff if hispserveyeardiff<0\n",
    "gen beforeafteryrs=0\n",
    "replace beforeafteryrs=hispserveyeardiff if hispserveyeardiff!=.\n",
    "    \n",
    "gen switchers = (hispserveyeardiff<.)\n",
    "    label var beforeyrs \"before years\"\n",
    "    label var afteryrs \"after years\"\n",
    "    label var beforeafteryrs \"before and after years\"\n",
    "    label var lnftfefwhitt \"white\"\n",
    "    label var lnftfefasiat \"Asian\"\n",
    "    label var lnftfefbkaat \"Black\"\n",
    "}\n",
    "sum beforeyrs afteryrs beforeafteryrs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      "\n",
      "\n",
      "\n",
      "white enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years             -0.037*       -0.013*       -0.016***                                          \n",
      "                        (0.016)       (0.006)       (0.003)                                             \n",
      "\n",
      "after years              -0.037*       -0.005        -0.018**       0.000         0.009        -0.002   \n",
      "                        (0.016)       (0.008)       (0.006)       (0.008)       (0.008)       (0.008)   \n",
      "\n",
      "before and after y~s                                               -0.037*       -0.014*       -0.016***\n",
      "                                                                  (0.016)       (0.006)       (0.003)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.54          0.43          0.46          0.54          0.43          0.46   \n",
      "schools                      74           220         1,129            74           220         1,129   \n",
      "N                         1,877         2,178        26,192         1,877         2,176        26,192   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "\n",
      "Asian enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years             -0.029        -0.000        -0.005                                             \n",
      "                        (0.018)       (0.013)       (0.005)                                             \n",
      "\n",
      "after years              -0.031         0.009        -0.011        -0.002         0.009        -0.006   \n",
      "                        (0.018)       (0.009)       (0.008)       (0.011)       (0.015)       (0.010)   \n",
      "\n",
      "before and after y~s                                               -0.029        -0.000        -0.005   \n",
      "                                                                  (0.018)       (0.013)       (0.005)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.33          0.25          0.22          0.33          0.25          0.22   \n",
      "schools                      73           214         1,078            73           214         1,078   \n",
      "N                         1,799         2,061        23,545         1,799         2,059        23,545   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "\n",
      "Black enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years             -0.021         0.016         0.002                                             \n",
      "                        (0.014)       (0.008)       (0.004)                                             \n",
      "\n",
      "after years              -0.031*        0.004        -0.012        -0.010        -0.012        -0.014   \n",
      "                        (0.016)       (0.008)       (0.006)       (0.007)       (0.009)       (0.008)   \n",
      "\n",
      "before and after y~s                                               -0.021         0.016         0.002   \n",
      "                                                                  (0.014)       (0.008)       (0.004)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.48          0.43          0.41          0.48          0.43          0.41   \n",
      "schools                      74           217         1,111            74           217         1,111   \n",
      "N                         1,880         2,173        26,158         1,880         2,171        26,158   \n",
      "--------------------------------------------------------------------------------------------------------\n"
     ]
    }
   ],
   "source": [
    "*** Check and clear xt\n",
    "\n",
    "    qui xtset\n",
    "    qui set line 105\n",
    "    qui set more off\n",
    "\n",
    " ** Private schools, Y is logged\n",
    "    foreach nam in ftfefwhitt ftfefasiat ftfefbkaat {\n",
    "        \n",
    "est clear\n",
    "        qui eststo SwPost: reghdfe ln`nam' beforeyrs afteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if switchers==1, absorb(unitid) cluster(unitid)\n",
    "\n",
    "        qui eststo counterPost: reghdfe ln`nam' beforeyrs afteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if pcttefhispt>=15, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo NSwPost: reghdfe ln`nam' beforeyrs afteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo Swdiff: reghdfe ln`nam' afteryrs beforeafteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if switchers==1, absorb(unitid) cluster(unitid)\n",
    "\n",
    "        qui eststo counterdiff: reghdfe ln`nam' afteryrs beforeafteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if pcttefhispt>15, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo NSwdiff: reghdfe ln`nam' afteryrs beforeafteryrs lnftfeftotlt lnpop* ///\n",
    "        lncpop* i.year , absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        esttab SwPost counterPost NSwPost Swdiff counterdiff NSwdiff, star(* .05 ** .01 *** .001) ///\n",
    "        stats(r2_a_within N_clust N, fmt(2 %9.0fc %9.0fc) label(\"r2\" \"schools\" \"N\")) ///\n",
    "        keep(beforeyrs afteryrs beforeafteryrs) se(%9.3fc) b(%9.3fc) ///\n",
    "        label mtitle(\"Switchers\" \"Counterfac\" \"All\" \"Switchers\" \"Counterfac \"\"All\") ///\n",
    "        prehead(\"`: var label ln`nam'' enrollment\") nonotes modelwidth(10)\n",
    "            \n",
    "   qui esttab SwPost counterPost NSwPost Swdiff counterdiff NSwdiff using tables/t3_private`nam'.rtf, star(* .05 ** .01 *** .001) ///\n",
    "        stats(r2_a_within N_clust N, fmt(2 %9.0fc %9.0fc) label(\"r2\" \"schools\" \"N\")) ///\n",
    "        keep(beforeyrs afteryrs beforeafteryrs) se(%9.3fc) b(%9.3fc) ///\n",
    "        label mtitle(\"Switchers\" \"Counter- factual\" \"All\" \"Switchers\" \"Counter- factual \"\"All\") replace ///\n",
    "        title(\"\\b `: var label ln`nam'' enrollment\") nonotes onecell compress\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Stata",
   "language": "stata",
   "name": "stata"
  },
  "language_info": {
   "codemirror_mode": "stata",
   "file_extension": ".do",
   "mimetype": "text/x-stata",
   "name": "stata",
   "version": "15.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
