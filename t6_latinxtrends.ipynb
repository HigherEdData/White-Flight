{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Table 6: Latinx First-Year Enrollments and Years Before and After HSI Status\n",
    "\n",
    "from White Flight in Higher Education? The Case of Hispanic-Serving Institutions\n",
    "\n",
    "by Laura Hamilton, Charlie Eaton, and Simon Cheng"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      ". sum beforeyrs afteryrs beforeafteryrs\n",
      "\n",
      "    Variable |        Obs        Mean    Std. Dev.       Min        Max\n",
      "-------------+---------------------------------------------------------\n",
      "   beforeyrs |     44,664   -.6454191     3.16297        -29          0\n",
      "    afteryrs |     44,664    .2218341    1.694023          0         28\n",
      "beforeafte~s |     44,664    -.423585    3.627734        -29         28\n"
     ]
    }
   ],
   "source": [
    "quietly {\n",
    "\n",
    "use data/hdef19902019, clear\n",
    "keep if ftfeftotlt>100 & ftfeftotlt<.\n",
    "\n",
    "**Create separate unitid for Benjamin Franklin Institute for when it goes from Public to Private\n",
    "    **Correct IPEDS reporting error to note it is private in 2016 & 2017\n",
    "replace unitid=unitid+1000000 if unitid==165884 & year>2007\n",
    "    replace control=2 if unitid==1165884\n",
    "    \n",
    "*replace Boricuo college Latinx enrollment as missing for years 1993 & 1994\n",
    "    * when reporting error reduced % Latinx from 95% to 0%\n",
    "replace tefhispt=. if unitid==189413 & year<1995 & year>1992\n",
    "    \n",
    "    gen pubpriv=\"public\"\n",
    "    replace pubpriv=\"private\" if control==2\n",
    "    \n",
    "foreach var in efbkaat efhispt efwhitt efaiant efasiat {\n",
    "quietly    gen pctt`var'= t`var' / teftotlt * 100\n",
    "quietly gen lnftf`var'=log(ftf`var')\n",
    "    quietly gen lnt`var'=log(t`var')\n",
    "}\n",
    "foreach var in cpopam cpopas cpopbl cpopwh cpophi populationam populationas populationbl populationhi populationwh {\n",
    "        gen ln`var'=log(`var')\n",
    "    }\n",
    "\n",
    "quietly gen lnftfeftotlt=log(ftfeftotlt)\n",
    "\n",
    "quietly gen hispserve=0 if pcttefhispt!=.\n",
    "quietly replace hispserve=1 if pcttefhispt>=25 & pcttefhispt<.\n",
    "\n",
    "xtset unitid year\n",
    "\n",
    "**create var for year in which event occurs observation year of event for subject**\n",
    "by unitid: gen transition=year if hispserv==1 & l.hispserv==0\n",
    "\n",
    "**create var for year in which event occurs in all observations for all years for subject**\n",
    "by unitid: egen hispserveyear = min(transition)\n",
    "\n",
    "**generate a var for the number of years relative to the year of the event (e.g. + or - 2 years)\n",
    "\n",
    "gen hispserveyeardiff= year-hispserveyear\n",
    "    \n",
    "**bin up year differences larger than absolute value of 10**\n",
    "*replace hispserveyeardiff =10 if hispserveyeardiff <9999 & hispserveyeardiff >10\n",
    "*replace hispserveyeardiff =-10 if hispserveyeardiff <-10\n",
    "    \n",
    "    gen afteryrs=0\n",
    "replace afteryrs=hispserveyeardiff if hispserveyeardiff>0 & hispserveyeardiff<.\n",
    "gen beforeyrs=0\n",
    "replace beforeyrs=hispserveyeardiff if hispserveyeardiff<0\n",
    "gen beforeafteryrs=0\n",
    "replace beforeafteryrs=hispserveyeardiff if hispserveyeardiff!=.\n",
    "    \n",
    "gen switchers = (hispserveyeardiff<.)\n",
    "    label var beforeyrs \"before years\"\n",
    "    label var afteryrs \"after years\"\n",
    "    label var beforeafteryrs \"before and after years\"\n",
    "    label var lnftfefwhitt \"white\"\n",
    "    label var lnftfefasiat \"Asian\"\n",
    "    label var lnftfefbkaat \"Black\"\n",
    "    label var admitpct \"% admitted\"\n",
    "}\n",
    "sum beforeyrs afteryrs beforeafteryrs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      "\n",
      "\n",
      "\n",
      "public Latinx enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years              0.022**       0.016***     -0.006*                                            \n",
      "                        (0.007)       (0.004)       (0.003)                                             \n",
      "\n",
      "after years               0.008         0.015***     -0.019***     -0.013***     -0.001        -0.013** \n",
      "                        (0.006)       (0.002)       (0.003)       (0.003)       (0.004)       (0.004)   \n",
      "\n",
      "before and after y~s                                                0.022**       0.016***     -0.006*  \n",
      "                                                                  (0.007)       (0.004)       (0.003)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.93          0.91          0.77          0.93          0.91          0.77   \n",
      "schools                      80           213           723            80           213           723   \n",
      "N                         2,005         2,579        17,167         2,005         2,579        17,167   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "\n",
      "private Latinx enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years              0.045***      0.029***      0.009*                                            \n",
      "                        (0.009)       (0.004)       (0.003)                                             \n",
      "\n",
      "after years               0.011         0.014***     -0.026***     -0.035***     -0.015**      -0.035***\n",
      "                        (0.010)       (0.004)       (0.005)       (0.006)       (0.005)       (0.006)   \n",
      "\n",
      "before and after y~s                                                0.045***      0.028***      0.009*  \n",
      "                                                                  (0.009)       (0.004)       (0.003)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.81          0.75          0.59          0.81          0.75          0.59   \n",
      "schools                      74           220         1,108            74           220         1,108   \n",
      "N                         1,880         2,184        25,255         1,880         2,182        25,255   \n",
      "--------------------------------------------------------------------------------------------------------\n"
     ]
    }
   ],
   "source": [
    "*** Check and clear xt\n",
    "\n",
    "    qui xtset\n",
    "    qui set line 105\n",
    "    qui set more off\n",
    "\n",
    "foreach pubpriv in public private {\n",
    "\n",
    "est clear\n",
    "        qui eststo SwPost: reghdfe lnftfefhispt beforeyrs afteryrs lnftfeftotlt  ///\n",
    "        lnpop* lncpop* i.year if switchers==1 & pubpriv==\"`pubpriv'\", absorb(unitid) cluster(unitid)\n",
    "\n",
    "        qui eststo counterPost: reghdfe lnftfefhispt beforeyrs afteryrs lnftfeftotlt  ///\n",
    "        lnpop* lncpop* i.year if pcttefhispt>=15 & pubpriv==\"`pubpriv'\", absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo NSwPost: reghdfe lnftfefhispt beforeyrs afteryrs lnftfeftotlt  ///\n",
    "        lnpop* lncpop* i.year if pubpriv==\"`pubpriv'\", absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo Swdiff: reghdfe lnftfefhispt afteryrs beforeafteryrs lnftfeftotlt  ///\n",
    "        lnpop* lncpop* i.year if switchers==1 & pubpriv==\"`pubpriv'\", absorb(unitid) cluster(unitid)\n",
    "\n",
    "        qui eststo counterdiff: reghdfe lnftfefhispt afteryrs beforeafteryrs lnftfeftotlt  ///\n",
    "        lnpop* lncpop* i.year if pcttefhispt>15 & pubpriv==\"`pubpriv'\", absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo NSwdiff: reghdfe lnftfefhispt afteryrs beforeafteryrs lnftfeftotlt  ///\n",
    "        lnpop* lncpop* i.year if pubpriv==\"`pubpriv'\", absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        esttab SwPost counterPost NSwPost Swdiff counterdiff NSwdiff, star(* .05 ** .01 *** .001) ///\n",
    "        stats(r2_a_within N_clust N, fmt(2 %9.0fc %9.0fc) label(\"r2\" \"schools\" \"N\")) ///\n",
    "        keep(beforeyrs afteryrs beforeafteryrs) se(%9.3fc) b(%9.3fc) ///        \n",
    "        order(beforeyrs afteryrs beforeafteryrs) ///\n",
    "        label mtitle(\"Switchers\" \"Counterfac\" \"All\" \"Switchers\" \"Counterfac \"\"All\") ///\n",
    "        prehead(\"`pubpriv' Latinx enrollment\") nonotes modelwidth(10)\n",
    "            \n",
    "   qui esttab SwPost counterPost NSwPost Swdiff counterdiff NSwdiff using tables/t6_latinx`pubpriv'.rtf, star(* .05 ** .01 *** .001) ///\n",
    "        stats(r2_a_within N_clust N, fmt(2 %9.0fc %9.0fc) label(\"r2\" \"schools\" \"N\")) ///\n",
    "        keep(beforeyrs afteryrs beforeafteryrs) se(%9.3fc) b(%9.3fc) ///\n",
    "        order(beforeyrs afteryrs beforeafteryrs) ///\n",
    "        label mtitle(\"Switchers\" \"Counter- factual\" \"All\" \"Switchers\" \"Counter- factual \"\"All\") replace ///\n",
    "        title(\"\\b `pubpriv' Latinx enrollment\") nonotes onecell compress\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Stata",
   "language": "stata",
   "name": "stata"
  },
  "language_info": {
   "codemirror_mode": "stata",
   "file_extension": ".do",
   "mimetype": "text/x-stata",
   "name": "stata",
   "version": "15.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
