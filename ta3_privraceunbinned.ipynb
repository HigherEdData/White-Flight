{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Appendix Table A3: Unbinned Trends in Non-Latinx First-Year Enrollments at Private Institutions\n",
    "\n",
    "from White Flight in Higher Education? The Case of Hispanic-Serving Institutions\n",
    "\n",
    "by Laura Hamilton, Charlie Eaton, and Simon Cheng"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      ". sum beforeyrs afteryrs beforeafteryrs\n",
      "\n",
      "    Variable |        Obs        Mean    Std. Dev.       Min        Max\n",
      "-------------+---------------------------------------------------------\n",
      "   beforeyrs |     44,664   -.6501209    3.173182        -29          0\n",
      "    afteryrs |     44,664    .2274987    1.718179          0         28\n",
      "beforeafte~s |     44,664   -.4226222    3.649251        -29         28\n"
     ]
    }
   ],
   "source": [
    "quietly {\n",
    "\n",
    "use /Users/Charlie/Dropbox/Stata_Notebooks/racedebtstrat/hdef19802019.dta, clear\n",
    "keep if control<3 & iclevel==1 & year>1989 & ftfeftotlt>100 & ftfeftotlt<.\n",
    "    \n",
    "foreach var in efbkaat efhispt efwhitt efaiant efasiat {\n",
    "quietly    gen pctt`var'= t`var' / teftotlt * 100\n",
    "quietly gen lnftf`var'=log(ftf`var')\n",
    "    quietly gen lnt`var'=log(t`var')\n",
    "}\n",
    "foreach var in cpopam cpopas cpopbl cpopwh cpophi populationam populationas populationbl populationhi populationwh {\n",
    "        gen ln`var'=log(`var')\n",
    "    }\n",
    "\n",
    "quietly gen lnftfeftotlt=log(ftfeftotlt)\n",
    "\n",
    "**More robust would be discontinuity\n",
    "quietly gen hispserve=0 if pcttefhispt!=.\n",
    "quietly replace hispserve=1 if pcttefhispt>=25 & pcttefhispt<.\n",
    "\n",
    "xtset unitid year\n",
    "\n",
    "**create var for year in which event occurs observation year of event for subject**\n",
    "by unitid: gen transition=year if hispserv==1 & l.hispserv==0\n",
    "\n",
    "**create var for year in which event occurs in all observations for all years for subject**\n",
    "by unitid: egen hispserveyear = min(transition)\n",
    "\n",
    "**generate a var for the number of years relative to the year of the event (e.g. + or - 2 years)\n",
    "\n",
    "gen hispserveyeardiff= year-hispserveyear\n",
    "    \n",
    "*SUPPRESS BINNING CODE BELOW bin up year differences larger than absolute value of 10**\n",
    "*replace hispserveyeardiff =10 if hispserveyeardiff <9999 & hispserveyeardiff >10\n",
    "*replace hispserveyeardiff =-10 if hispserveyeardiff <-10\n",
    "    \n",
    "    gen afteryrs=0\n",
    "replace afteryrs=hispserveyeardiff if hispserveyeardiff>0 & hispserveyeardiff<.\n",
    "gen beforeyrs=0\n",
    "replace beforeyrs=hispserveyeardiff if hispserveyeardiff<0\n",
    "gen beforeafteryrs=0\n",
    "replace beforeafteryrs=hispserveyeardiff if hispserveyeardiff!=.\n",
    "    \n",
    "gen switchers = (hispserveyeardiff<.)\n",
    "    label var beforeyrs \"before years\"\n",
    "    label var afteryrs \"after years\"\n",
    "    label var beforeafteryrs \"before and after years\"\n",
    "    label var lnftfefwhitt \"white\"\n",
    "    label var lnftfefasiat \"Asian\"\n",
    "    label var lnftfefbkaat \"Black\"\n",
    "}\n",
    "sum beforeyrs afteryrs beforeafteryrs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      "\n",
      "\n",
      "\n",
      "white enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years             -0.037*       -0.016**      -0.016***                                          \n",
      "                        (0.016)       (0.005)       (0.003)                                             \n",
      "\n",
      "after years              -0.040*       -0.009        -0.020**      -0.003         0.007        -0.004   \n",
      "                        (0.016)       (0.007)       (0.006)       (0.009)       (0.008)       (0.008)   \n",
      "\n",
      "before and after y~s                                               -0.037*       -0.016**      -0.016***\n",
      "                                                                  (0.016)       (0.005)       (0.003)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.51          0.44          0.46          0.51          0.44          0.46   \n",
      "schools                      75           220         1,129            75           220         1,129   \n",
      "N                         1,899         2,175        26,190         1,899         2,173        26,190   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "\n",
      "Asian enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years             -0.030         0.000        -0.005                                             \n",
      "                        (0.018)       (0.013)       (0.005)                                             \n",
      "\n",
      "after years              -0.031         0.010        -0.011        -0.002         0.010        -0.006   \n",
      "                        (0.018)       (0.009)       (0.008)       (0.011)       (0.015)       (0.010)   \n",
      "\n",
      "before and after y~s                                               -0.030         0.000        -0.005   \n",
      "                                                                  (0.018)       (0.013)       (0.005)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.32          0.25          0.22          0.32          0.25          0.22   \n",
      "schools                      74           214         1,078            74           214         1,078   \n",
      "N                         1,807         2,058        23,543         1,807         2,056        23,543   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "\n",
      "Black enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years             -0.022         0.020**       0.002                                             \n",
      "                        (0.014)       (0.008)       (0.004)                                             \n",
      "\n",
      "after years              -0.029         0.012        -0.008        -0.006        -0.008        -0.010   \n",
      "                        (0.016)       (0.009)       (0.007)       (0.008)       (0.010)       (0.008)   \n",
      "\n",
      "before and after y~s                                               -0.022         0.019*        0.002   \n",
      "                                                                  (0.014)       (0.008)       (0.004)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.47          0.44          0.41          0.47          0.44          0.41   \n",
      "schools                      75           217         1,111            75           217         1,111   \n",
      "N                         1,905         2,170        26,156         1,905         2,168        26,156   \n",
      "--------------------------------------------------------------------------------------------------------\n"
     ]
    }
   ],
   "source": [
    "*** Check and clear xt\n",
    "\n",
    "    qui xtset\n",
    "    qui set line 105\n",
    "    qui set more off\n",
    "\n",
    " ** Public schools, Y is logged\n",
    "    foreach nam in ftfefwhitt ftfefasiat ftfefbkaat {\n",
    "        \n",
    "est clear\n",
    "        qui eststo SwPost: reghdfe ln`nam' beforeyrs afteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if switchers==1 & control==2, absorb(unitid) cluster(unitid)\n",
    "\n",
    "        qui eststo counterPost: reghdfe ln`nam' beforeyrs afteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if control==2 & pcttefhispt>=15, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo NSwPost: reghdfe ln`nam' beforeyrs afteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if control==2, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo Swdiff: reghdfe ln`nam' afteryrs beforeafteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if switchers==1 & control==2, absorb(unitid) cluster(unitid)\n",
    "\n",
    "        qui eststo counterdiff: reghdfe ln`nam' afteryrs beforeafteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if control==2 & pcttefhispt>15, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo NSwdiff: reghdfe ln`nam' afteryrs beforeafteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if control==2, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        esttab SwPost counterPost NSwPost Swdiff counterdiff NSwdiff, star(* .05 ** .01 *** .001) ///\n",
    "        stats(r2_a_within N_clust N, fmt(2 %9.0fc %9.0fc) label(\"r2\" \"schools\" \"N\")) ///\n",
    "        keep(beforeyrs afteryrs beforeafteryrs) se(%9.3fc) b(%9.3fc) ///\n",
    "        label mtitle(\"Switchers\" \"Counterfac\" \"All\" \"Switchers\" \"Counterfac \"\"All\") ///\n",
    "        prehead(\"`: var label ln`nam'' enrollment\") nonotes modelwidth(10)\n",
    "            \n",
    "   qui esttab SwPost counterPost NSwPost Swdiff counterdiff NSwdiff using tables/ta3_privunbinned`nam'.rtf, star(* .05 ** .01 *** .001) ///\n",
    "        stats(r2_a_within N_clust N, fmt(2 %9.0fc %9.0fc) label(\"r2\" \"schools\" \"N\")) ///\n",
    "        keep(beforeyrs afteryrs beforeafteryrs) se(%9.3fc) b(%9.3fc) ///\n",
    "        label mtitle(\"Switchers\" \"Counter- factual\" \"All\" \"Switchers\" \"Counter- factual \"\"All\") replace ///\n",
    "        title(\"\\b `: var label ln`nam'' enrollment\") nonotes onecell compress\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Stata",
   "language": "stata",
   "name": "stata"
  },
  "language_info": {
   "codemirror_mode": "stata",
   "file_extension": ".do",
   "mimetype": "text/x-stata",
   "name": "stata",
   "version": "15.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
