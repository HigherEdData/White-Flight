{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Table 2: Non-Latinx First-Year Enrollments and Years Before and After HSI Status at Public Institutions\n",
    "\n",
    "from White Flight in Higher Education? The Case of Hispanic-Serving Institutions\n",
    "\n",
    "by Laura Hamilton, Charlie Eaton, and Simon Cheng"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      ". sum beforeyrs afteryrs beforeafteryrs\n",
      "\n",
      "    Variable |        Obs        Mean    Std. Dev.       Min        Max\n",
      "-------------+---------------------------------------------------------\n",
      "   beforeyrs |     44,664   -.6501209    3.173182        -29          0\n",
      "    afteryrs |     44,664    .2274987    1.718179          0         28\n",
      "beforeafte~s |     44,664   -.4226222    3.649251        -29         28\n"
     ]
    }
   ],
   "source": [
    "quietly {\n",
    "\n",
    "use data/hdef19902019, clear\n",
    "keep if ftfeftotlt>100 & ftfeftotlt<.\n",
    "    \n",
    "foreach var in efbkaat efhispt efwhitt efaiant efasiat {\n",
    "quietly    gen pctt`var'= t`var' / teftotlt * 100\n",
    "quietly gen lnftf`var'=log(ftf`var')\n",
    "    quietly gen lnt`var'=log(t`var')\n",
    "}\n",
    "foreach var in cpopam cpopas cpopbl cpopwh cpophi populationam populationas populationbl populationhi populationwh {\n",
    "        gen ln`var'=log(`var')\n",
    "    }\n",
    "\n",
    "quietly gen lnftfeftotlt=log(ftfeftotlt)\n",
    "\n",
    "quietly gen hispserve=0 if pcttefhispt!=.\n",
    "quietly replace hispserve=1 if pcttefhispt>=25 & pcttefhispt<.\n",
    "\n",
    "xtset unitid year\n",
    "\n",
    "**create var for year in which event occurs observation year of event for subject**\n",
    "by unitid: gen transition=year if hispserv==1 & l.hispserv==0\n",
    "\n",
    "**create var for year in which event occurs in all observations for all years for subject**\n",
    "by unitid: egen hispserveyear = min(transition)\n",
    "\n",
    "**generate a var for the number of years relative to the year of the event (e.g. + or - 2 years)\n",
    "\n",
    "gen hispserveyeardiff= year-hispserveyear\n",
    "    \n",
    "*bin up year differences larger than absolute value of 10**\n",
    "*replace hispserveyeardiff =10 if hispserveyeardiff <9999 & hispserveyeardiff >10\n",
    "*replace hispserveyeardiff =-10 if hispserveyeardiff <-10\n",
    "    \n",
    "    gen afteryrs=0\n",
    "replace afteryrs=hispserveyeardiff if hispserveyeardiff>0 & hispserveyeardiff<.\n",
    "gen beforeyrs=0\n",
    "replace beforeyrs=hispserveyeardiff if hispserveyeardiff<0\n",
    "gen beforeafteryrs=0\n",
    "replace beforeafteryrs=hispserveyeardiff if hispserveyeardiff!=.\n",
    "    \n",
    "gen switchers = (hispserveyeardiff<.)\n",
    "    label var beforeyrs \"before years\"\n",
    "    label var afteryrs \"after years\"\n",
    "    label var beforeafteryrs \"before and after years\"\n",
    "    label var lnftfefwhitt \"white\"\n",
    "    label var lnftfefasiat \"Asian\"\n",
    "    label var lnftfefbkaat \"Black\"\n",
    "}\n",
    "sum beforeyrs afteryrs beforeafteryrs"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\n",
      "\n",
      "\n",
      "\n",
      "\n",
      "white enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years             -0.005         0.009        -0.007**                                           \n",
      "                        (0.008)       (0.005)       (0.002)                                             \n",
      "\n",
      "after years              -0.026***     -0.015**      -0.030***     -0.021***     -0.024***     -0.023***\n",
      "                        (0.007)       (0.005)       (0.004)       (0.004)       (0.005)       (0.004)   \n",
      "\n",
      "before and after y~s                                               -0.005         0.009        -0.007** \n",
      "                                                                  (0.008)       (0.005)       (0.002)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.58          0.60          0.48          0.58          0.60          0.48   \n",
      "schools                      81           213           724            81           213           724   \n",
      "N                         2,021         2,581        17,481         2,021         2,581        17,481   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "\n",
      "Asian enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years             -0.016        -0.003        -0.001                                             \n",
      "                        (0.009)       (0.008)       (0.003)                                             \n",
      "\n",
      "after years              -0.029**      -0.019*       -0.019**      -0.013        -0.015        -0.017** \n",
      "                        (0.009)       (0.009)       (0.006)       (0.007)       (0.009)       (0.007)   \n",
      "\n",
      "before and after y~s                                               -0.016        -0.003        -0.001   \n",
      "                                                                  (0.009)       (0.008)       (0.003)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.61          0.47          0.38          0.61          0.47          0.38   \n",
      "schools                      81           213           719            81           213           719   \n",
      "N                         2,007         2,541        16,676         2,007         2,541        16,676   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "\n",
      "Black enrollment\n",
      "                            (1)           (2)           (3)           (4)           (5)           (6)   \n",
      "                      Switchers    Counterfac           All     Switchers    Counterfac           All   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "before years             -0.001         0.008        -0.008*                                            \n",
      "                        (0.012)       (0.008)       (0.003)                                             \n",
      "\n",
      "after years              -0.012         0.000        -0.021**      -0.011        -0.008        -0.014   \n",
      "                        (0.010)       (0.009)       (0.007)       (0.008)       (0.011)       (0.008)   \n",
      "\n",
      "before and after y~s                                               -0.001         0.008        -0.008*  \n",
      "                                                                  (0.012)       (0.008)       (0.003)   \n",
      "--------------------------------------------------------------------------------------------------------\n",
      "r2                         0.55          0.44          0.47          0.55          0.44          0.47   \n",
      "schools                      81           213           723            81           213           723   \n",
      "N                         2,014         2,564        17,309         2,014         2,564        17,309   \n",
      "--------------------------------------------------------------------------------------------------------\n"
     ]
    }
   ],
   "source": [
    "*** Check and clear xt\n",
    "\n",
    "    qui xtset\n",
    "    qui set line 105\n",
    "    qui set more off\n",
    "\n",
    " ** Public schools, Y is logged\n",
    "    foreach nam in ftfefwhitt ftfefasiat ftfefbkaat {\n",
    "        \n",
    "est clear\n",
    "        qui eststo SwPost: reghdfe ln`nam' beforeyrs afteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if switchers==1 & control==1, absorb(unitid) cluster(unitid)\n",
    "\n",
    "        qui eststo counterPost: reghdfe ln`nam' beforeyrs afteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if control==1 & pcttefhispt>=15, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo NSwPost: reghdfe ln`nam' beforeyrs afteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if control==1, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo Swdiff: reghdfe ln`nam' afteryrs beforeafteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if switchers==1 & control==1, absorb(unitid) cluster(unitid)\n",
    "\n",
    "        qui eststo counterdiff: reghdfe ln`nam' afteryrs beforeafteryrs lnftfeftotlt ///\n",
    "        lnpop* lncpop* i.year if control==1 & pcttefhispt>15, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        qui eststo NSwdiff: reghdfe ln`nam' afteryrs beforeafteryrs lnftfeftotlt lnpop* ///\n",
    "        lncpop* i.year if control==1, absorb(unitid) cluster(unitid)\n",
    "        \n",
    "        esttab SwPost counterPost NSwPost Swdiff counterdiff NSwdiff, star(* .05 ** .01 *** .001) ///\n",
    "        stats(r2_a_within N_clust N, fmt(2 %9.0fc %9.0fc) label(\"r2\" \"schools\" \"N\")) ///\n",
    "        keep(beforeyrs afteryrs beforeafteryrs) se(%9.3fc) b(%9.3fc) ///\n",
    "        label mtitle(\"Switchers\" \"Counterfac\" \"All\" \"Switchers\" \"Counterfac \"\"All\") ///\n",
    "        prehead(\"`: var label ln`nam'' enrollment\") nonotes modelwidth(10)\n",
    "            \n",
    "   qui esttab SwPost counterPost NSwPost Swdiff counterdiff NSwdiff using tables/t2_public`nam'.rtf, star(* .05 ** .01 *** .001) ///\n",
    "        stats(r2_a_within N_clust N, fmt(2 %9.0fc %9.0fc) label(\"r2\" \"schools\" \"N\")) ///\n",
    "        keep(beforeyrs afteryrs beforeafteryrs) se(%9.3fc) b(%9.3fc) ///\n",
    "        label mtitle(\"Switchers\" \"Counter- factual\" \"All\" \"Switchers\" \"Counter- factual \"\"All\") replace ///\n",
    "        title(\"\\b `: var label ln`nam'' enrollment\") nonotes onecell compress\n",
    "}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Stata",
   "language": "stata",
   "name": "stata"
  },
  "language_info": {
   "codemirror_mode": "stata",
   "file_extension": ".do",
   "mimetype": "text/x-stata",
   "name": "stata",
   "version": "15.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
